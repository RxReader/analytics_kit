group 'io.github.v7lin.analytics_kit'
version '0.0.1'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()

        // 私仓
        flatDir {
            dirs project(':analytics_kit').file('libs')
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16

        // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
        consumerProguardFiles 'consumer-rules.pro'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    flavorDimensions 'vendor'

    productFlavors {
        vendor {
            dimension 'vendor'

            // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
            consumerProguardFiles 'consumer-vendor-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.mergeAssetsProvider.get().doLast{ task ->
            println("try to delete 'supplierconfig.json' from library")
            delete(fileTree(task.outputDir) { config ->
                config.include("supplierconfig.json")
            })
        }
    }
}

dependencies {
    //
    vendorImplementation(name: 'msa_mdid_1.0.13', ext: 'aar')
    vendorImplementation 'com.baidu.mobstat:mtj-sdk:4.0.4.6'
}
